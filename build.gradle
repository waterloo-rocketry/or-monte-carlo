plugins {
    id 'java'
}

group 'com.waterloorocketry'
version '0.1'

ext {
    openRocketVersion = '24.12.RC.01'
}

// Detect OS and set the appropriate gradlew command
def gradleWrapper = System.getProperty('os.name').toLowerCase().contains('windows') ? 'gradlew.bat' : './gradlew'

repositories {
    flatDir {
        dirs 'openrocket/build/libs' // Point to the submodule's output
    }
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['or-monte-carlo/src']  // Using list syntax for consistency
        }
    }
}

dependencies {
    implementation name: 'OpenRocket', version: openRocketVersion, group: 'info.openrocket'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

// Java configuration
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    // Ensure consistent encoding and compatibility
    compileJava {
        options.encoding = 'UTF-8'
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

tasks.register('buildOpenRocket', Exec) {
    workingDir 'openrocket'
    def jarFile = file("build/libs/OpenRocket-${openRocketVersion}.jar")

    commandLine gradleWrapper, 'build', '-x', 'test'

    onlyIf {
        !jarFile.exists()
    }
}

tasks.register('cleanOpenRocket', Exec) {
    workingDir 'openrocket'

    commandLine gradleWrapper, 'clean'
}

tasks.register('runOpenRocket', JavaExec) {
    dependsOn buildOpenRocket
    classpath = files("openrocket/build/libs/OpenRocket-${openRocketVersion}.jar")
    mainClass = 'info.openrocket.swing.startup.SwingStartup'
}

// Make sure the submodule builds before the main build
tasks.named('build') {
    dependsOn buildOpenRocket
}

tasks.named('clean') {
    dependsOn cleanOpenRocket
}

tasks.register('run', JavaExec) {
    dependsOn build
    mainClass = 'com.waterloorocketry.openrocket_monte_carlo.Main'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'java.library.path', 'build/natives/lib'
}