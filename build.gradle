plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'application'
    id "io.freefair.aspectj" version "9.1.0"
}

group 'com.waterloorocketry'
version '1.4.0'

ext {
    openRocketVersion = '24.12'
}

application {
    mainClass = "com.waterloorocketry.openrocket_monte_carlo.Main"
}

repositories {
    flatDir {
        dirs 'openrocket/build/libs'
    }
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['or-monte-carlo/src']
        }
        resources {
            srcDirs = ['or-monte-carlo/src/main/resources']
        }
    }
}

// disable extensions from running as well when calling 'run' on the main project
subprojects {
    tasks.matching { it.name == 'run' }.configureEach {
        enabled = false
    }
}

dependencies {
    implementation "info.openrocket:OpenRocket:${openRocketVersion}"

    subprojects.each { p ->
        implementation project(p.path)
    }

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

    implementation 'org.aspectj:aspectjrt:1.9.25'
    implementation 'org.aspectj:aspectjtools:1.9.25'

    inpath files("openrocket/build/libs/OpenRocket-${openRocketVersion}.jar")


}


// Java configuration
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    // Ensure consistent encoding and compatibility
    compileJava {
        options.encoding = 'UTF-8'
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

compileAspectj {
    ajcOptions {
        verbose = true
        compilerArgs = ['-Xlint:cantFindType=warning']
    }
    options {

    }
}

tasks.register('buildExtensions') {
    description = 'Build all extensions'
    group = 'build'
    subprojects.each { p ->
        dependsOn p.tasks.named('build')
    }
}

tasks.register('cleanExtensions') {
    description = 'Cleans all extensions'
    group = 'clean'
    subprojects.each { p ->
        dependsOn p.tasks.named('clean')
    }
}

tasks.register('cleanAll') {
    description = 'Cleans this project, its subprojects, and all extensions'
    group = 'clean'
    dependsOn tasks.named('clean')
    dependsOn cleanExtensions
}


tasks.named('run', JavaExec) {
    dependsOn build
    systemProperties = System.properties as Map<String, ?>

    mainClass = 'com.waterloorocketry.openrocket_monte_carlo.Main'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'java.library.path', 'build/natives/lib'
}


shadowJar {
    dependsOn(distTar, distZip)
    archiveClassifier.set("all")
    archiveVersion.set(version.toString())
    archiveBaseName.set("WaterlooRocketry-OpenRocket")
}
